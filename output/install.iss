; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Weasel"

#ifndef WEASEL_VERSION
    #define WEASEL_VERSION="0.14.3"
#endif

#ifndef WEASEL_BUILD
    #define WEASEL_BUILD="0"
#endif
#define MyAppPublisher "式恕堂"
#define MyAppURL "https://rime.im"
#define MyAppExeName "WeaselServer.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={#MyAppName}
AppName={#MyAppName}
AppVersion={#WEASEL_VERSION}
;AppVerName={#MyAppName} {#WEASEL_VERSION}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\Rime\{#MyAppName}-{#WEASEL_VERSION}
DefaultGroupName=小狼毫输入法
UninstallDisplayName=小狼毫输入法
DisableProgramGroupPage=yes
LicenseFile=LICENSE.txt
SetupIconFile=..\resource\{#MyAppName}.ico
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=archives
OutputBaseFilename={#MyAppName}-{#WEASEL_VERSION}.{#WEASEL_BUILD}-installer-inno
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "chinesesimplified"; MessagesFile: "compiler:Languages/ChineseSimplified.isl"

[Files]
Source: "LICENSE.txt"; DestDir: "{app}";
Source: "README.txt"; DestDir: "{app}"; Flags: isreadme
Source: "7-zip-license.txt"; DestDir: "{app}";
Source: "7z.dll"; DestDir: "{app}";
Source: "7z.exe"; DestDir: "{app}";
Source: "COPYING-curl.txt"; DestDir: "{app}";
Source: "curl.exe"; DestDir: "{app}";
Source: "curl-ca-bundle.crt"; DestDir: "{app}";
Source: "rime-install.bat"; DestDir: "{app}";
Source: "rime-install-config.bat"; DestDir: "{app}";
Source: "weasel.dll"; DestDir: "{app}";
Source: "weaselt.dll"; DestDir: "{app}";
Source: "weasel.ime"; DestDir: "{app}";
Source: "weaselt.ime"; DestDir: "{app}";
Source: "weaselx64.dll"; DestDir: "{app}"; Flags: 64bit
Source: "weaseltx64.dll"; DestDir: "{app}"; Flags: 64bit
Source: "weaselx64.ime"; DestDir: "{app}"; Flags: 64bit
Source: "weaseltx64.ime"; DestDir: "{app}"; Flags: 64bit
Source: "WeaselDeployer.exe"; DestDir: "{app}";
Source: "WeaselServer.exe"; DestDir: "{app}";
Source: "WeaselSetup.exe"; DestDir: "{app}";
Source: "rime.dll"; DestDir: "{app}";
Source: "WinSparkle.dll"; DestDir: "{app}";
; shared data files
Source: "data\*.yaml"; DestDir: "{app}\data"; Flags: ignoreversion
Source: "data\*.txt"; DestDir: "{app}\data"; Flags: ignoreversion skipifsourcedoesntexist 
Source: "data\*.gram"; DestDir: "{app}\data"; Flags: ignoreversion skipifsourcedoesntexist 
; opencc data files
Source: "data\opencc\*.json"; DestDir: "{app}\data\opencc"; Flags: ignoreversion
Source: "data\opencc\*.ocd*"; DestDir: "{app}\data\opencc"; Flags: ignoreversion
Source: "data\opencc\*.txt"; DestDir: "{app}\data\opencc"; Flags: ignoreversion skipifsourcedoesntexist
; images
Source: "data\preview\*.png"; DestDir: "{app}\data\preview"; Flags: ignoreversion

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\【小狼毫】說明書";Filename: "{app}\README.txt"
Name: "{group}\【小狼毫】輸入法設定" ;Filename:"{app}\WeaselDeployer.exe"
Name: "{group}\【小狼毫】用戶詞典管理"; Filename:"{app}\WeaselDeployer.exe"; Parameters: "/dict"
Name: "{group}\【小狼毫】用戶資料同步"; Filename: "{app}\WeaselDeployer.exe"; Parameters: "/sync"
Name: "{group}\【小狼毫】重新部署"; Filename: "{app}\WeaselDeployer.exe"; Parameters:  "/deploy"
Name: "{group}\小狼毫算法服務"; Filename: "{app}\WeaselServer.exe"
Name: "{group}\【小狼毫】用戶文件夾"; Filename: "{app}\WeaselServer.exe"; Parameters: "/userdir"
Name: "{group}\【小狼毫】程序文件夾"; Filename:"{app}\WeaselServer.exe"; Parameters: "/weaseldir"
Name: "{group}\【小狼毫】檢查新版本"; Filename: "{app}\WeaselServer.exe"; Parameters: "/update"
Name: "{group}\【小狼毫】安裝選項"; Filename: "{app}\WeaselSetup.exe"
Name: "{group}\{cm:UninstallProgram,}小狼毫"; Filename:"{uninstallexe}"

[Registry]
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "WeaselServer"; ValueData: "{app}\{#MyAppExeName}"; Flags: uninsdeletekey

[UninstallRun]
Filename: "{app}\WeaselServer.exe"; Parameters: "/quit";RunOnceId: "RimeStopService"
Filename: "{app}\WeaselSetup.exe"; Parameters: "/u";RunOnceId: "RimeUninstallService"

[Run]
Filename: "{app}\WeaselSetup.exe";
Filename: "{app}\WeaselDeployer.exe"; Parameters: "/install"
Filename: "{app}\WeaselServer.exe"; Flags: nowait
